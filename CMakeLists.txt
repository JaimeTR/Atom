cmake_minimum_required(VERSION 3.5)
project(AtomLang)

list(INSERT CMAKE_MODULE_PATH 0 "${AtomLang_SOURCE_DIR}/cmake/Modules")

find_package(LLVM HINTS ${LLVM_HINTS} REQUIRED)
find_package(Clang REQUIRED)
find_package(Git) 

if(AtomLang_ENABLE_CUDA)
  find_package(CUDA REQUIRED)
elseif(NOT DEFINED AtomLang_ENABLE_CUDA)
  find_package(CUDA)
endif()
set(AtomLang_ENABLE_CUDA ${CUDA_FOUND} CACHE BOOL "Build AtomLang with support for CUDA")

if(DEFINED AtomLang_STATIC_LINK_LLVM)
  set(DEFAULT_AtomLang_SLIB_INCLUDE_LLVM ${AtomLang_STATIC_LINK_LLVM})
else()
  if((LLVM_VERSION_MAJOR GREATER 6 AND LLVM_VERSION_MAJOR LESS 8) OR WIN32)
    set(DEFAULT_AtomLang_SLIB_INCLUDE_LLVM OFF)
  else()
    set(DEFAULT_AtomLang_SLIB_INCLUDE_LLVM ON)
  endif()
endif()
set(AtomLang_SLIB_INCLUDE_LLVM ${DEFAULT_AtomLang_SLIB_INCLUDE_LLVM} CACHE BOOL "Include LLVM in AtomLang static libraries")
set(AtomLang_STATIC_LINK_LLVM ON CACHE BOOL "Statically link AtomLang against LLVM")

if(AtomLang_SLIB_INCLUDE_LLVM AND NOT AtomLang_STATIC_LINK_LLVM)
  message(FATAL_ERROR "AtomLang_SLIB_INCLUDE_LLVM requires AtomLang_STATIC_LINK_LLVM to be set")
endif()

if((LLVM_VERSION_MAJOR GREATER 6 AND LLVM_VERSION_MAJOR LESS 8) AND AtomLang_SLIB_INCLUDE_LLVM)
  message(FATAL_ERROR "AtomLang_SLIB_INCLUDE_LLVM is not supported in LLVM version 7")
endif()

if(WIN32 AND AtomLang_SLIB_INCLUDE_LLVM)
  message(FATAL_ERROR "AtomLang_SLIB_INCLUDE_LLVM is not supported on Windows")
endif()

if(DEFINED AtomLang_STATIC_LINK_LUAJIT)
  set(DEFAULT_AtomLang_SLIB_INCLUDE_LUAJIT ${AtomLang_STATIC_LINK_LUAJIT})
else()
  if(WIN32)
    set(DEFAULT_AtomLang_SLIB_INCLUDE_LUAJIT OFF)
  else()
    set(DEFAULT_AtomLang_SLIB_INCLUDE_LUAJIT ON)
  endif()
endif()
set(AtomLang_SLIB_INCLUDE_LUAJIT ${DEFAULT_AtomLang_SLIB_INCLUDE_LUAJIT} CACHE BOOL "Include LuaJIT in AtomLang static libraries")
set(AtomLang_STATIC_LINK_LUAJIT ON CACHE BOOL "Statically link AtomLang against LuaJIT")

if(AtomLang_SLIB_INCLUDE_LUAJIT AND NOT AtomLang_STATIC_LINK_LUAJIT)
  message(FATAL_ERROR "AtomLang_SLIB_INCLUDE_LUAJIT requires AtomLang_STATIC_LINK_LUAJIT to be set")
endif()

if(WIN32 AND AtomLang_SLIB_INCLUDE_LUAJIT)
  message(FATAL_ERROR "AtomLang_SLIB_INCLUDE_LUAJIT is not supported on Windows")
endif()

include(GNUInstallDirs)
include(VersionNumber)
include(BuildDirs)
include(GetLuaJIT)
include(ExtractLLVM)

if(LLVM_VERSION_MAJOR GREATER 9)
  set(CMAKE_CXX_STANDARD 14)
else()
  set(CMAKE_CXX_STANDARD 11)
endif()
set(CMAKE_CXX_STANDARD_REQUIRED ON)

enable_testing()

add_subdirectory(src)